// EXPERIENCE

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,31999)
  XPLT(Myself,32000)
THEN
  RESPONSE #100
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,32000,SET)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,15999)
  XPLT(Myself,16000)
THEN
  RESPONSE #100
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,16000,SET)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,7999)
  XPLT(Myself,8000)
THEN
  RESPONSE #100
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,8000,SET)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,3999)
  XPLT(Myself,4000)
THEN
  RESPONSE #100
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,4000,SET)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,1999)
  XPLT(Myself,2000)
THEN
  RESPONSE #100
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,2000,SET)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPLT(Player1,2000)
THEN
  RESPONSE #100
    SetGlobal("BD_JOINXP","LOCALS",1)
	ChangeStat(Myself,XP,1000,SET)
END

//Approval Shifts 

IF 
InParty(Myself)
Switch("X3HelgaAppChange","GLOBAL")
THEN RESPONSE #1
SetGlobal("X3HelgaAppChange","GLOBAL",0)
IncrementGlobal("X3HelgaApp","GLOBAL",-10)
DisplayStringNoNameDlg(Player1,@200109)
RESPONSE #2
SetGlobal("X3HelgaAppChange","GLOBAL",0)
IncrementGlobal("X3HelgaApp","GLOBAL",-8)
DisplayStringNoNameDlg(Player1,@200109)
RESPONSE #3
SetGlobal("X3HelgaAppChange","GLOBAL",0)
IncrementGlobal("X3HelgaApp","GLOBAL",-6)
DisplayStringNoNameDlg(Player1,@200106)
RESPONSE #4
SetGlobal("X3HelgaAppChange","GLOBAL",0)
IncrementGlobal("X3HelgaApp","GLOBAL",-4)
DisplayStringNoNameDlg(Player1,@200106)
RESPONSE #5
SetGlobal("X3HelgaAppChange","GLOBAL",0)
IncrementGlobal("X3HelgaApp","GLOBAL",-2)
DisplayStringNoNameDlg(Player1,@200103)
RESPONSE #6
SetGlobal("X3HelgaAppChange","GLOBAL",0)
RESPONSE #7
SetGlobal("X3HelgaAppChange","GLOBAL",0)
IncrementGlobal("X3HelgaApp","GLOBAL",2)
DisplayStringNoNameDlg(Player1,@200113)
RESPONSE #8
SetGlobal("X3HelgaAppChange","GLOBAL",0)
IncrementGlobal("X3HelgaApp","GLOBAL",4)
DisplayStringNoNameDlg(Player1,@200116)
RESPONSE #9
SetGlobal("X3HelgaAppChange","GLOBAL",0)
IncrementGlobal("X3HelgaApp","GLOBAL",6)
DisplayStringNoNameDlg(Player1,@200116)
RESPONSE #10
SetGlobal("X3HelgaAppChange","GLOBAL",0)
IncrementGlobal("X3HelgaApp","GLOBAL",8)
DisplayStringNoNameDlg(Player1,@200119)
RESPONSE #11
SetGlobal("X3HelgaAppChange","GLOBAL",0)
IncrementGlobal("X3HelgaApp","GLOBAL",10)
DisplayStringNoNameDlg(Player1,@200119)
END 

// If not in party, it should be reset with no approval changes. 
IF 
!InParty(Myself) 
!Global("X3HelgaAppChange","GLOBAL",0)
THEN RESPONSE #100 
SetGlobal("X3HelgaAppChange","GLOBAL",0)
END

//Reset to 120 if past max/reward bonus stats.
IF 
GlobalGT("X3HelgaApp","GLOBAL",120)
InParty(Myself)
Switch("X3HelgaAppMaxBonus","LOCALS")
THEN RESPONSE #0
SetGlobal("X3HelgaApp","GLOBAL",120)
SetGlobal("X3HelgaAppMaxBonus","LOCALS",1)
ChangeStat(Myself,WIS,1,ADD)
DisplayStringNoNameDlg(Player1,@20099)
RESPONSE #1 
SetGlobal("X3HelgaApp","GLOBAL",120)
END 

IF 
GlobalLT("X3HelgaApp","GLOBAL",-39)
InParty(Myself)
Switch("X3Leave","LOCALS")
THEN RESPONSE #0
SetGlobal("X3Break","LOCALS",1)
StartDialogueNoSet(Player1)
RESPONSE #2
SetGlobal("X3Break","LOCALS",3)
StartDialogueNoSet(Player1) // Leaves for Good
END


//Does Helga's quest after recruiting. 
IF 
InParty("X3Helga")
!Dead("X3HogreL")
Global("X3HelgaTimerInitiate","GLOBAL",0)
THEN RESPONSE #100 
SmallWait(1)
IncrementGlobal("X3HelgaTimerInitiate","GLOBAL",1)
RealSetGlobalTimer("X3HelgaTalkTimer","GLOBAL",15)
SetGlobalTimer("X3HelgaQuestTen","GLOBAL",TEN_DAYS)
SmallWait(1)
SetGlobal("X3HelgaQuestExpire","GLOBAL",1)
IncrementGlobal("X3HelgaTalk","GLOBAL",1)
END 
//Does Helga's quest before recruiting. 
IF 
InParty("X3Helga")
Dead("X3HogreL")
Global("X3HelgaTimerInitiate","GLOBAL",0)
THEN RESPONSE #100 
IncrementGlobal("X3HelgaTimerInitiate","GLOBAL",1)
RealSetGlobalTimer("X3HelgaTalkTimer","GLOBAL",15)
IncrementGlobal("X3HelgaTalk","GLOBAL",1)
END 
//The Ogre Has Died!
IF 
Dead("X3HogreL")
Global("X3HelgaQuest","GLOBAL",0)
CombatCounter(0)
InParty("X3Helga")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
!See([ENEMY])
THEN RESPONSE #100 
SetGlobal("X3HelgaQuest","GLOBAL",1)
StartDialogueNoSet(Player1)
END 
// Quest Timers, these are Ten Days each, for a total of a 30 day time limit.
//Increment Block
IF 
InParty(Myself)
GlobalTimerExpired("X3HelgaQuestTen","GLOBAL")
!Dead("X3HogreL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(3)
Global("X3HelgaQuestExpire","GLOBAL",1)
Global("X3HelgaQuestExpire","GLOBAL",3)
Global("X3HelgaQuestExpire","GLOBAL",5)
THEN RESPONSE #100
IncrementGlobal("X3HelgaQuestExpire","GLOBAL",1)
END 
// Dialogue Block
IF 
!AreaCheck("%bg1_eet_symbol%4600")
InParty(Myself)
GlobalTimerExpired("X3HelgaQuestTen","GLOBAL")
!Dead("X3HogreL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(3)
Global("X3HelgaQuestExpire","GLOBAL",2)
Global("X3HelgaQuestExpire","GLOBAL",4)
Global("X3HelgaQuestExpire","GLOBAL",6)
THEN RESPONSE #100
StartDialogueNoSet(Player1)
END 

//Talk Block 
IF
InParty(Myself)
RealGlobalTimerExpired("X3HelgaTalkTimer","GLOBAL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)    
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)                          
!See([ENEMY])
!AreaType(DUNGEON)			  
OR(5)                       // YOU WILL NEED TO ADD +1 TO THIS OR NUMBER FOR EVERY ADDITONAL TALK YOU ADD          
Global("X3HelgaTalk","GLOBAL",3)
Global("X3HelgaTalk","GLOBAL",5)
Global("X3HelgaTalk","GLOBAL",7)
Global("X3HelgaTalk","GLOBAL",9)
Global("X3HelgaTalk","GLOBAL",11)
THEN
RESPONSE #100
IncrementGlobal("X3HelgaTalk","GLOBAL",1)
END


IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
OR(5)                                    // YOU WILL NEED TO ADD +1 TO THIS OR NUMBER FOR EVERY ADDITONAL TALK YOU ADD  
Global("X3HelgaTalk","GLOBAL",4)
Global("X3HelgaTalk","GLOBAL",6)
Global("X3HelgaTalk","GLOBAL",8)
Global("X3HelgaTalk","GLOBAL",10)
Global("X3HelgaTalk","GLOBAL",12)
THEN
RESPONSE #100
PlaySong(0)
//PlaySound("X3HSonD") 
StartDialogueNoSet(Player1)
END
// Durlag Talk 
IF
	InParty(Myself)
	AreaCheck("%bg1_eet_symbol%0511")	
	Global("X3HelgaDurlag","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(1)
	SetGlobal("X3HelgaDurlag","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Rage 
IF 
HPPercentLT(Myself,20)
Global("X3HelRage","LOCALS",0)
THEN RESPONSE #100 
SetGlobal("X3HelRage","LOCALS",1)
ForceSpell(Myself,BERSERKER_RAGE)
END 	

IF 
PartyRested()
Global("X3HelRage","LOCALS",1)
THEN RESPONSE #100 
SetGlobal("X3HelRage","LOCALS",0)
END 

// SEX CHANGE

IF
    InParty(Myself)
	Gender(Myself,MALE)
	HasItemEquiped("belt05",Myself)
    Global("X3HelgaSexChange","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(1)
	SetGlobal("X3HelgaSexChange","GLOBAL",1)
	StartDialogueNoSet(Player1)
END